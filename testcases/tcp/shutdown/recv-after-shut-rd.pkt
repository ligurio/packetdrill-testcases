// SPDX-License-Identifier: BSD-2-Clause
// Copyright (c) 2015, Michael Tuexen
// Source: https://github.com/nplab/misc-tcp-testscripts
// Create a blocking listening TCP socket.
 0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
+0.000 bind(3, ..., ...) = 0
+0.000 listen(3, 1) = 0
// Establish the TCP connection.
+0.050 < S  0:0(0) win 65535
+0.000 > S. 0:0(0) ack 1 <...>
+0.050 <  . 1:1(0) ack 1 win 65535
+0.000 accept(3, ..., ...) = 4
+0.000 close(3) = 0
// Shutdown the reading side.
+0.100 shutdown(4, SHUT_RD) = 0
+0.100 recv(4, ..., 1000, 0) = 0
// Now receive some data.
+0.100 <  . 1:1001(1000) ack 1 win 65535
+0.000 >  . 1:1(0) ack 1001
+0.100 recv(4, ..., 1000, 0) = 0
// Teardown the TCP connection.
+1.000 close(4) = 0 
+0.000 > F. 1:1(0) ack 1001
+0.050 < F. 1001:1001(0) ack 2 win 65535 
+0.000 >  . 2:2(0) ack 1002 
