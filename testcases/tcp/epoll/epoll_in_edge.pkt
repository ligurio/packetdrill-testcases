// Test the behavior of EPOLLET for EPOLLIN.
//
// If new data comes in, epoll_wait returns EPOLLIN regardless of whether
// we have drained the socket or not.
`../common/defaults.sh`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < S 0:0(0) win 32792 <mss 1000,sackOK,nop,nop,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,nop,sackOK,nop,wscale 8>
   +0 < . 1:1(0) ack 1 win 257

   +0 accept(3, ..., ...) = 4

   +0 fcntl(4, F_SETFL, O_RDWR|O_NONBLOCK) = 0
   +0 epoll_create(1) = 5

   // Add the FD as EPOLLET and clear all the events.
   +0 epoll_ctl(5, EPOLL_CTL_ADD, 4, {events=EPOLLOUT|EPOLLIN|EPOLLET, fd=4}) = 0
   +0 epoll_wait(5, {events=EPOLLOUT, fd=4}, 1, 0) = 1
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // We have 4KB of data ready to read so we get EPOLLIN.
   +0 < P. 1:4001(4000) ack 1 win 257
   +0 > . 1:1(0) ack 4001
   +0 epoll_wait(5, {events=EPOLLIN|EPOLLOUT, fd=4}, 1, 0) = 1
   // Now epoll_wait should not return any event because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // We have 6KB of data ready to read so we get EPOLLIN.
   +0 < P. 4001:10001(6000) ack 1 win 257
   +0 > . 1:1(0) ack 10001
   +0 epoll_wait(5, {events=EPOLLIN|EPOLLOUT, fd=4}, 1, 0) = 1
   // Now epoll_wait should not return any event because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // Read some data from the socket, but do not drain it.
   +0 read(4, ..., 1000) = 1000
   // epoll_wait should still return no events because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // We have 2KB of data ready to read so we get EPOLLIN.
   +0 < P. 10001:12001(2000) ack 1 win 257
   +0 > . 1:1(0) ack 12001
   +0 epoll_wait(5, {events=EPOLLIN|EPOLLOUT, fd=4}, 1, 0) = 1
   // Now epoll_wait should not return any event because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0
