// SPDX-License-Identifier: MIT
// Copyright (c) 2016 Shivansh Rai
// Source: https://github.com/freebsd-net/TCP-IP-Regression-TestSuite
// Borrowed from https://github.com/google/packetdrill
// Test behavior when a listener gets an incoming packet from new
// sender on a non-established session that has the SYN and RST bits set.
// The listener should not respond to avoid infinite RST ping-pong storms.
// Checking if we can establish a connection with the client
// after a connection reset.
--tolerance_usecs=100000

// Create a listening TCP socket
0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

0.100 < SR 0:0(0) win 65535 <mss 1460,sackOK,nop,nop,nop,wscale 7>

// The TCP stack should not respond to incoming RSTs, or else
// we could get infinite RST ping-pong storms.

// Now make sure that when a valid SYN arrives shortly thereafter
// (with the same address 4-tuple) we can still successfully establish
// a connection.

0.200 < S 0:0(0) win 65535 <mss 1460,sackOK,nop,nop,nop,wscale 7>
0.200 > S. 0:0(0) ack 1 <...>

0.300 < . 1:1(0) ack 1 win 65535
0.300 accept(3, ..., ...) = 4
