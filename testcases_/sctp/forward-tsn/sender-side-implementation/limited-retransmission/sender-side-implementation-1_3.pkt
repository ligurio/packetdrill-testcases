// SPDX-License-Identifier: BSD-3-Clause
// Copyright (c) 2016, nplab
// Source: https://github.com/nplab/PR_SCTP_Testsuite
//
// Copyright (c) 2016 Julian Cordes
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
//
// THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
// ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
// FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
// DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
// OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
// HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
// LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
// OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
// SUCH DAMAGE.
//

--tolerance_usecs=250000
#ifdef FreeBSD
// only enable PR-SCTP extension on FreeBSD
 0.0 `sysctl -i net.inet.sctp.ecn_enable=0`
+0.0 `sysctl -i net.inet.sctp.pr_enable=1`
+0.0 `sysctl -i net.inet.sctp.asconf_enable=0`
+0.0 `sysctl -i net.inet.sctp.pktdrop_enable=0`
+0.0 `sysctl -i net.inet.sctp.reconfig_enable=0`
+0.0 `sysctl -i net.inet.sctp.auth_enable=0`    // FreeBSD 11 and higher
+0.0 `sysctl -i net.inet.sctp.auth_disable=1`   // FreeBSD 9.x and 10.x
+0.0 `sysctl -i net.inet.sctp.nrsack_enable=0`  // FreeBSD 11 and higher
+0.0 `sysctl -i net.inet.sctp.nrsack_on_off=0`  // FreeBSD 9.x and 10.x
#endif

#ifdef Linux
// only enable PR-SCTP extension on Linux
 0.0 `sysctl -ew net.sctp.auth_enable=0`
+0.0 `sysctl -ew net.sctp.prsctp_enable=1`
#endif

+0.0 socket(..., SOCK_STREAM, IPPROTO_SCTP) = 3
+0.0 fcntl(3, F_GETFL) = 0x2 (flags O_RDWR)
+0.0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0 bind(3, ..., ...) = 0
+0.1 connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)
#ifdef FreeBSD
+0.0 > sctp: INIT[flgs=0, tag=1, a_rwnd=..., os=..., is=..., tsn=1, FORWARD_TSN_SUPPORTED[], 
                                                                    SUPPORTED_EXTENSIONS[types=[0xc0]], 
                                                                    SUPPORTED_ADDRESS_TYPES[types=[IPv4]]]
#endif
#ifdef Linux
+0.0 > sctp: INIT[flgs=0, tag=1, a_rwnd=..., os=..., is=..., tsn=1, SUPPORTED_ADDRESS_TYPES[types=[IPv4]], 
                                                                    ECN_CAPABLE[], 
                                                                    FORWARD_TSN_SUPPORTED[]]
#endif
+0.1 < sctp: INIT_ACK[flgs=0, tag=2, a_rwnd=1500, os=16, is=16, tsn=1, STATE_COOKIE[len=4, val=...],
                                                                       FORWARD_TSN_SUPPORTED[]]
+0.0 > sctp: COOKIE_ECHO[flgs=0, len=4, val=...]
+0.1 < sctp: COOKIE_ACK[flgs=0]
+0.0 getsockopt(3, SOL_SOCKET, SO_ERROR, [0], [4]) = 0
// Turn off the sending of HEARTBEATs.
+0.0 setsockopt(3, IPPROTO_SCTP, SCTP_PEER_ADDR_PARAMS, {spp_address=..., spp_hbinterval=0, spp_pathmaxrxt=0, spp_pathmtu=0, spp_flags=SPP_HB_DISABLE|SPP_PMTUD_DISABLE, spp_ipv6_flowlabel=0, spp_dscp=0}, 152) = 0
+0.0 sctp_sendv(3, [{..., 3000}], 1, ..., 1, 
                    {sendv_flags=SCTP_SEND_SNDINFO_VALID|SCTP_SEND_PRINFO_VALID,
                     sendv_sndinfo={snd_sid=1, snd_flags=0, snd_ppid=htonl(1234), snd_context=0, snd_assoc_id=0},
                     sendv_prinfo={pr_policy=SCTP_PR_SCTP_RTX, pr_value=2},
                     sendv_authinfo={auth_keynumber=123}}, 32, SCTP_SENDV_SPA, 0) = 3000
+0.0 > sctp: DATA[flgs=B, len=1468, tsn=1, sid=1, ssn=0, ppid=1234]
+0.1 < sctp: SACK[flgs=0, cum_tsn=1, a_rwnd=10000, gaps=[], dups=[]]
+0.0 > sctp: DATA[flgs=0, len=1468, tsn=2, sid=1, ssn=0, ppid=1234]
+0.0 > sctp: DATA[flgs=E, len=112, tsn=3, sid=1, ssn=0, ppid=1234]
+0.1 < sctp: SACK[flgs=0, cum_tsn=1, a_rwnd=10000, gaps=[2:2], dups=[]]
// 1. retransmission
+0.9 > sctp: DATA[flgs=0, len=1468, tsn=2, sid=1, ssn=0, ppid=1234]
// 2. retransmission
+2.0 > sctp: DATA[flgs=0, len=1468, tsn=2, sid=1, ssn=0, ppid=1234]
// now expect outgoing forward-tsn-chunk with cum_tsn=3, stream_identifier=1 and stream_seq_number=0
#ifdef FreeBSD
+4.0 > sctp: FORWARD_TSN[cum_tsn=3, ids=[{1,0}, {1, 0}]]
#endif
#ifdef Linux
+4.0 > sctp: FORWARD_TSN[cum_tsn=3, ids=[{1,0}]]
#endif
+0.1 < sctp: SACK[flgs=0, cum_tsn=3, a_rwnd=10000, gaps=[], dups=[]]
// check if the cwnd at the IUT did not advance and is 2...
+0.0 sctp_sendmsg(3, ..., 3000, ..., ..., htonl(1234), 0, 1, 0, 0) = 3000
+0.0 > sctp: DATA[flgs=B, len=1468, tsn=4, sid=1, ssn=1, ppid=1234]
+0.0 > sctp: DATA[flgs=0, len=1468, tsn=5, sid=1, ssn=1, ppid=1234]
+0.1 < sctp: SACK[flgs=0, cum_tsn=5, a_rwnd=10000, gaps=[], dups=[]]
+0.0 > sctp: DATA[flgs=E, len=112, tsn=6, sid=1, ssn=1, ppid=1234]
+0.1 < sctp: SACK[flgs=0, cum_tsn=6, a_rwnd=10000, gaps=[], dups=[]]
+1.0 close(3) = 0
+0.0 > sctp: SHUTDOWN[flgs=0, cum_tsn=0]
+0.1 < sctp: SHUTDOWN_ACK[flgs=0]
+0.0 > sctp: SHUTDOWN_COMPLETE[flgs=0]
